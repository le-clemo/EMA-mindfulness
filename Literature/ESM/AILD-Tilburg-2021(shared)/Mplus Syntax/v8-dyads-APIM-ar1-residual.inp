TITLE:    Ch.8 dyads example; 
DATA:     FILE IS dyadsdata.dat;
VARIABLE: NAMES ARE coupleid personid time time7c freldis fwrkstrs fwrkstrsc 
          fwrkstrscb fwrkstrscw mreldis mwrkstrs mwrkstrsc mwrkstrscb mwrkstrscw;
          USEVAR =  time7c freldis mreldis fwrkstrscw mwrkstrscw 
                    mwrkstrscb fwrkstrscb;
          BETWEEN = mwrkstrscb fwrkstrscb;
          WITHIN =  time7c mwrkstrscw fwrkstrscw;
          CLUSTER = coupleid;
          LAGGED = freldis(1) mreldis(1);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR=bayes;
	        PROCESSORS = 4; 
	        BITERATIONS = (5000);
MODEL:    %WITHIN% 
           cf | freldis ON fwrkstrscw; 
           cm | mreldis ON mwrkstrscw;
           cfm | freldis ON mwrkstrscw;
           cmf | mreldis ON fwrkstrscw;
           freldis^ ON freldis^1;
           mreldis^ ON mreldis^1;
           freldis mreldis ON time7c;
           freldis (freldisv1); mreldis (mreldisv1);
           freldis WITH mreldis (covlev1);
          %BETWEEN%
           freldis ON fwrkstrscb mwrkstrscb;
           mreldis ON mwrkstrscb fwrkstrscb;           
           freldis mreldis cf cm cfm cmf WITH freldis mreldis cf cm cfm cmf;
           freldis WITH cf cm;
           mreldis WITH cf cm;
           freldis WITH mreldis (covint);
           cf WITH cm (covslp);
           freldis (freldisv2);
           mreldis (mreldisv2);
           cf (cf);
           cm (cm);
          MODEL CONSTRAINT:
           NEW rint; rint = covint/sqrt(freldisv2 * mreldisv2);
           NEW rslp; rslp = covslp/sqrt(cf * cm);
           NEW rlev1; rlev1 = covlev1/sqrt(freldisv1 * mreldisv1);
OUTPUT:    cinterval sampstat;